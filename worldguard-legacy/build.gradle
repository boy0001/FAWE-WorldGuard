apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    maven { url "http://repo.bstats.org/content/repositories/releases/" }
}

dependencies {
    compile project(':worldguard-core')
    compile 'com.google.guava:guava:21.0'
    compile 'com.boydti:fawe-bukkit:latest'
    compile 'org.bukkit:bukkit:1.13-R0.1-SNAPSHOT'
    compile ('com.sk89q:commandbook:2.3') {
        exclude group: 'com.sk89q', module: 'worldedit'
        exclude group: 'com.zachsthings.libcomponents'
        transitive = false
    }
    compile 'org.bstats:bstats-bukkit:1.2'
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-library:1.2.1'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        expand project.properties
        include 'plugin.yml'
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

jar.archiveName="worldguard-legacy-latest.jar"
jar.destinationDir = file '../mvn/com/sk89q/worldguard-legacy/latest'
task createPom << {
    pom {
        project {
            groupId 'com.sk89q'
            artifactId 'worldguard-legacy'
            version 'latest'
        }
    }
    .getEffectivePom()
            .setDependencies(new ArrayList<>())
            .writeTo("../mvn/com/sk89q/worldguard-legacy/latest/worldguard-legacy-latest.pom")
}


shadowJar {
    dependencies {
        include(dependency(':worldguard-core'))
        relocate ("org.bstats", "com.sk89q.worldguard.bukkit.bstats") {
            include(dependency("org.bstats:bstats-bukkit:1.2"))
        }
    }
    

    relocate('org.flywaydb', 'com.sk89q.worldguard.internal.flywaydb')
    relocate('com.sk89q.squirrelid', 'com.sk89q.worldguard.util.profile')
    relocate('org.json.simple', 'com.sk89q.worldguard.util.jsonsimple')

}

build.dependsOn(shadowJar)
build.finalizedBy(createPom)
